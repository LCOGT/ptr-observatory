10 REM    SKYCAT.BAS -- PROGRAM TO LIST ALL STARS IN THE SKYCAT.DAT 
20 REM          FILE THAT LIE WITHIN A SELECTED WINDOW OF SKY
30 REM             Copyright (c) 1989 Sky Publishing Corp.
40 REM                  Version of October 21, 1989
50 REM
60 GOSUB 1530
70 CLS
80 PRINT TAB(20);"SKY CATALOGUE 2000.0, Vol. 1"
90 PRINT: PRINT
100 PRINT "This program finds all stars within a selected window of sky."
110 PRINT
120 PRINT "Your options:"
130 PRINT "   (1) List stars with physical data"
140 PRINT "   (2) List stars with names (if any)"
150 PRINT "   (3) Exit
160 PRINT
170 INPUT "Choice";Q$: Q=VAL(Q$)
180 IF Q<1 OR Q>3 THEN 170
190 IF Q<>INT(Q) THEN 170
200 IF Q=3 THEN 1730
210 IF Q=1 OR Q=2 THEN GOSUB 270
220 REM
230 INPUT "Finished.  Return to menu";Q$
240 GOTO 70
250 REM
260 REM
270 REM   FIND STARS WITHIN A SELECTED WINDOW
280 REM
290 CLS
300 INPUT "Desired Right Ascension (hours, min, sec)";H,M,S
310 IF H<0 OR H>=24 THEN 300
320 IF M<0 OR M>=60 THEN 300
330 IF S<0 OR S>=60 THEN 300
340 H0=H+M/60+S/3600: IF H0>=24 THEN H0=H0-24:  REM  R.A. IN HOURS
350 REM
360 INPUT "        Declination (degrees, min, sec)  ";D5$,M1,S1
370 S=1: IF LEFT$(D5$,1)="-" THEN S=-1
380 D1=ABS(VAL(D5$))
390 IF D1>90 THEN 360
400 IF M1<0 OR M1>=60 THEN 360
410 IF S1<0 OR S1>=60 THEN 360
420 IF D1=90 AND (M1>0 OR S1>0) THEN 360
430 D0=S*(D1+M1/60+S1/3600): REM  DECLINATION IN DEGREES
440 INPUT "        Window diameter (degrees)       ";W$
450 W=VAL(W$): IF W<=0 THEN 440
460 IF W<.05 THEN W=.05
470 IF W>10 THEN PRINT "Too big (must not exceed 10 degrees)": GOTO 440
480 WR=W/2: REM  WINDOW RADIUS IN DEGREES
490 DH=SIN(WR*DR)/COS(D0*DR)
500 SD=SIN(D0*DR): CD=COS(D0*DR)
510 PC=0: IF DH>1 THEN H1=0: H2=23.9999: PC=1: GOTO 560
520 DH=ATN(DH/SQR(1-DH*DH))/(DR*15)
530 H1=H0-DH: IF H1<0 THEN H1=H1+24: REM  STARTING R.A.
540 H2=H0+DH: IF H2>=24 THEN H2=H2-24: REM  ENDING R.A.
550 REM
560 H=INT(H1): M1=60*(H1-H): M=INT(M1): S=60*(M1-M)
570 GOSUB 1180: C1$=C0$
580 H=INT(H2): M1=60*(H2-H): M=INT(M1): S=60*(M1-M)
590 GOSUB 1180: C2$=C0$
600 C0$=C1$: GOSUB 1340
610 K=-1: T=0: CLS: GOSUB 1280
620 K=K+1: IF K<18 THEN 650
630 PRINT:INPUT "Hit <ENTER> key for more.  Ready";Q$
640 K=0: CLS: GOSUB 1280
650 GET#1,I: C3$=A$
660 IF PC=1 AND I=I9 THEN 890
670 IF C2$>=C1$ AND C3$>C2$ THEN 890
680 IF C2$<C1$ AND C3$>C2$ AND C3$<"1159599" THEN 890
690 D8$=LEFT$(D$,3): M8$=MID$(D$,4,2): S8$=RIGHT$(D$,2)
700 S=1: IF LEFT$(D8$,1)="-" OR MID$(D8$,2,1)="-" THEN S=-1
710 D=S*(ABS(VAL(D8$))+VAL(M8$)/60+VAL(S8$)/3600)
720 IF ABS(D-D0)<=WR THEN 760: REM  OBJECT IS IN ROUGHLY THE RIGHT
730 REM                             RANGE OF R.A. AND DEC.
740 I=I+1: IF I>I9 THEN I=I-I9
750 GOTO 650
760 REM    IS THE OBJECT REALLY WITHIN ANGULAR DISTANCE WR?
770 H6$=LEFT$(A$,2): M6$=MID$(A$,3,2): S6$=MID$(A$,5,3)
780 H=VAL(H6$)+VAL(M6$)/60+VAL(S6$)/36000!: REM s6$ is in 10ths of a sec.
790 CS=SIN(D*DR)*SD+COS(D*DR)*CD*COS((H-H0)*15*DR)
800 IF CS>1 THEN CS=1
810 SP=ATN(SQR(1-CS*CS)/CS)/DR: REM  SP=SEPARATION IN DEGREES
820 IF SP>WR THEN 740
830 IF N$="6565" THEN 740: REM   Skip 'deleted' stars <<<<<
840 GOSUB 930: T=T+1
850 I=I+1: IF I>I9 THEN I=I-I9
860 GOTO 620
870 REM
880 REM
890 IF T=0 THEN PRINT "No stars found.": PRINT
900 PRINT
910 RETURN
920 REM
930 REM   PRINT A LINE OF DATA
940 REM
950 S5$=SA$: T5$=T$: R5$=RV$
960 A5$=LEFT$(A$,2)+" "+MID$(A$,3,2)+" "+MID$(A$,5,2)+"."+RIGHT$(A$,1)
970 D5$=LEFT$(D$,3)+" "+MID$(D$,4,2)+" "+RIGHT$(D$,2)
980 A6$=LEFT$(A1$,2)+"."+RIGHT$(A1$,3): IF A6$="  .   " THEN A6$="      "
990 D6$=LEFT$(D1$,2)+"."+RIGHT$(D1$,2): IF D6$="  .  " THEN D6$="     "
1000 V5$=LEFT$(V$,2)+"."+RIGHT$(V$,2)
1010 B5$=LEFT$(BV$,2)+"."+RIGHT$(BV$,2): IF B5$="  .  " THEN B5$="     "
1020 P5$=P$+" ": P1$=RIGHT$(P5$,2): P2$=""
1030 IF P1$="h " THEN P2$="mx"
1040 IF P1$="l " THEN P2$="mn"
1050 IF P1$="c " THEN P2$="ts"
1060 IF P2$<>"" THEN P5$=LEFT$(P5$,4)+P2$
1070 N1$="   ": IF N$<>"    " THEN N1$="  *"
1080 REM
1090 PRINT S5$;"  ";A5$;"  ";D5$;"  ";: IF Q=2 THEN 1120
1100 PRINT A6$;" ";D6$;"  ";V5$;" ";B5$;"  ";T5$;" "R5$;" ";P5$;N1$
1110 GOTO 1160
1120 NM$=" ": NV=VAL(N$)
1130 IF NV=0 THEN 1150
1140 GET#2,NV: NM$=NA$
1150 PRINT V5$;"       ";NM$
1160 RETURN
1170 REM
1180 REM  EXPRESS R.A. AS A STRING
1190 REM   -- ENTER WITH H,M,S; EXIT WITH C0$
1200 REM
1210 PH$=MID$(STR$(100+H),3,2)
1220 PM$=MID$(STR$(100+M),3,2)
1230 PS$=MID$(STR$(1000+S*10),3,3)
1240 C0$=PH$+PM$+PS$
1250 IF LEFT$(C0$,1)="0" THEN C0$=" "+RIGHT$(C0$,6)
1260 RETURN
1270 REM
1280 REM   SELECT AND DISPLAY PROPER HEADING
1290 REM
1300 IF Q=1 THEN PRINT H1$: PRINT H2$: GOTO 1320
1310 PRINT H3$: PRINT H4$: REM   Heading for names
1320 RETURN
1330 REM
1340 REM  SEEK SUBROUTINE -- ENTER WITH C0$; EXIT WITH I SET TO
1350 REM       THE RECORD NUMBER JUST BEFORE THE DESIRED R.A.
1360 REM
1370 I0=1: I9=45269!
1380 I=INT(I9/2): REM  START IN MIDDLE OF FILE
1390 D=I
1400 D=INT(D/2)
1410 IF D=0 THEN 1460
1420 GET#1,I: P9$=A$
1430 S=1: IF P9$>C0$ THEN S=-1
1440 I=I+S*D
1450 GOTO 1400
1460 REM
1470 IF I=I9 THEN 1500
1480 IF A$<C0$ THEN I=I+1: GET#1,I: GOTO 1470
1490 I=I-1: IF I>0 THEN GET#1,I
1500 IF A$>=C0$ AND I>1 THEN 1490
1510 RETURN
1520 REM
1530 REM      OPEN FILES AND SET UP HEADING
1540 REM
1550 DR=3.14159265#/180: REM  DEGREES TO RADIANS
1560 S$=CHR$(34): REM  Symbol for arc seconds (")
1570 H1$=" SAO       R.A.        Dec.     PM(a)  PM(d)"
1580 H1$=H1$+"   V    B-V   Sp   RV  Dist"
1590 H2$="         h  m  s      o  '  "+S$+"    s      "+S$
1600 H2$=H2$+"                     km/s  pc"
1610 H3$=" SAO       R.A.        Dec.      V         Star name(s)"
1620 H4$="         h  m  s      o  '  "+S$
1630 F1$="SKYCAT.DAT"
1640 OPEN "R",1,F1$,52
1650 FIELD 1,6 AS SA$,7 AS A$,7 AS D$,5 AS A1$,4 AS D1$,4 AS V$
1660 FIELD 1,33 AS X$,4 AS BV$,2 AS T$,4 AS RV$,5 AS P$,4 AS N$
1670 REM
1680 F2$="NAMES.DAT"
1690 OPEN "R",2,F2$,33
1700 FIELD 2,33 AS NA$
1710 RETURN
1720 REM
1730 REM   COMMON EXIT
1740 CLOSE
1750 REM
1760 END
