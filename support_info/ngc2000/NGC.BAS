10 REM    NGC.BAS -- PROGRAM TO READ NGC 2000.0
20 REM    Copyright (c) 1989 Sky Publishing Corp.
30 REM    Version of Jan. 18, 1989
40 REM
50 DR=3.14159265#/180: REM  DEGREES TO RADIANS
60 HD$="NGC/IC   R.A.    Dec.   Size  Mag.    Type"
70 F1$="NGC2000.DAT"
80 OPEN "R",1,F1$,24
90 FIELD 1,5 AS N$,1 AS T$,2 AS H$,3 AS M$
100 FIELD 1,11 AS X$,3 AS D$,2 AS M1$,4 AS S$,4 AS B$
110 FIELD 1, 6 AS X1$,5 AS P$
120 F2$="INDEX.DAT"
130 OPEN "R",2,F2$,2
140 FIELD 2,2 AS V$
150 REM
160 CLS
170 PRINT TAB(30);"NGC 2000.0"
180 PRINT: PRINT
190 PRINT "Your options:"
200 PRINT
210 PRINT "   (1) List objects by NGC or IC number"
220 PRINT "   (2) Find all objects within a selected window"
230 PRINT "   (3) Exit
240 PRINT
250 INPUT "Choice";Q$: Q=VAL(Q$)
260 IF Q<1 OR Q>3 THEN 250
270 IF Q<>INT(Q) THEN 250
280 IF Q=3 THEN 1710
290 IF Q=1 THEN GOSUB 360
300 IF Q=2 THEN GOSUB 580
310 REM
320 INPUT "Finished.  Return to menu";Q$
330 GOTO 160
340 REM
350 REM
360 REM   LIST OBJECTS BY NGC OR IC NUMBER
370 REM
380 CLS: INPUT "NGC (N) OR IC (I)";Q$
390 IF Q$<>"N" AND Q$<>"n" AND Q$<>"I" AND Q$<>"i" THEN 380
400 IF Q$="i" THEN Q$="I"
410 IF Q$="n" THEN Q$="N"
420 INPUT "START, END";S,E
430 IF Q$="I" AND E>5386 THEN 420
440 IF S<1 OR S>E OR E>7840 THEN 420
450 IF Q$="I" THEN S=S+7840: E=E+7840
460 K=-1: CLS: PRINT HD$: PRINT
470 FOR I=S TO E
480 GET#2,I: V=CVI(V$)
490 K=K+1: IF K<18 THEN 520
500 PRINT: INPUT "Hit <ENTER> key for more.  Ready";Q$
510 K=0: CLS: PRINT HD$: PRINT
520 GET#1,V
530 GOSUB 1190
540 NEXT
550 PRINT
560 RETURN
570 REM
580 REM   FIND OBJECTS WITHIN A SELECTED WINDOW
590 REM
600 CLS
610 INPUT "Desired Right Ascension (hours, minutes)";H,M
620 IF H<0 OR H>23 THEN 610
630 IF M<0 OR M>=60 THEN 610
640 IF H<>INT(H) THEN 610
650 H0=H+M/60: REM  R.A. IN HOURS
660 REM
670 INPUT "        Declination (degrees, minutes)  ";D5$,M
680 S=1: IF LEFT$(D5$,1)="-" THEN S=-1
690 D=ABS(VAL(D5$))
700 IF D>90 THEN 670
710 IF M<0 OR M>=60 THEN 670
720 IF D=90 AND M>0 THEN 670
730 D0=S*(D+M/60): REM  DECLINATION IN DEGREES
740 INPUT "        Window diameter (degrees)       ";W$
750 W=VAL(W$): IF W<=0 THEN 740
760 IF W<.05 THEN W=.05
770 IF W>10 THEN PRINT "Too big (must not exceed 10 degrees)": GOTO 740
780 WR=W/2: REM  WINDOW RADIUS IN DEGREES
790 DH=SIN(WR*DR)/COS(D0*DR)
800 SD=SIN(D0*DR): CD=COS(D0*DR)
810 PC=0: IF DH>1 THEN H1=0: H2=23.9999: PC=1: GOTO 860
820 DH=ATN(DH/SQR(1-DH*DH))/(DR*15)
830 H1=H0-DH: IF H1<0 THEN H1=H1+24: REM  STARTING R.A.
840 H2=H0+DH: IF H2>=24 THEN H2=H2-24: REM  ENDING R.A.
850 REM
860 H=INT(H1): M=60*(H1-H): GOSUB 1440: P1$=P0$
870 H=INT(H2): M=60*(H2-H): GOSUB 1440: P2$=P0$
880 P0$=P1$: GOSUB 1520
890 K=-1: T=0: CLS: PRINT HD$: PRINT
900 K=K+1: IF K<18 THEN 930
910 PRINT:INPUT "Hit <ENTER> key for more.  Ready";Q$
920 K=0: CLS: PRINT HD$: PRINT
930 GET#1,I: P5$=P$
940 IF PC=1 AND I=I9 THEN 1150
950 IF P2$>=P1$ AND P5$>P2$ THEN 1150
960 IF P2$<P1$ AND P5$>P2$ AND P5$<"11599" THEN 1150
970 S=1: IF LEFT$(D$,1)="-" THEN S=-1
980 D=S*(ABS(VAL(D$))+VAL(M1$)/60)
990 IF ABS(D-D0)<=WR THEN 1030: REM  OBJECT IS IN ROUGHLY THE RIGHT
1000 REM                              RANGE OF R.A. AND DEC.
1010 I=I+1: IF I>I9 THEN I=I-I9
1020 GOTO 930
1030 REM    IS THE OBJECT REALLY WITHIN ANGULAR DISTANCE WR?
1040 M6$=M$: IF RIGHT$(M6$,1)=" " THEN M6$=LEFT$(M6$,2)+"0"
1050 H=VAL(H$)+VAL(M6$)/600
1060 CS=SIN(D*DR)*SD+COS(D*DR)*CD*COS((H-H0)*15*DR)
1070 IF CS>1 THEN CS=1
1080 SP=ATN(SQR(1-CS*CS)/CS)/DR: REM  SP=SEPARATION IN DEGREES
1090 IF SP>WR THEN 1010
1100 GOSUB 1190: T=T+1
1110 I=I+1: IF I>I9 THEN I=I-I9
1120 GOTO 900
1130 REM
1140 REM
1150 IF T=0 THEN PRINT "No objects found.": PRINT
1160 PRINT
1170 RETURN
1180 REM
1190 REM   DECODE THE DATA
1200 REM
1210 M5$=LEFT$(M$,2)+"."+RIGHT$(M$,1)
1220 RA$=H$+" "+M5$: REM  RIGHT ASCENSION
1230 DC$=D$+" "+M1$: REM  DECLINATION
1240 TY$=" "
1250 IF T$="X" THEN TY$="Galaxy": GOTO 1410
1260 IF T$="-" THEN TY$="Nonexistent in RNGC": GOTO 1410
1270 IF T$="N" THEN TY$="Nebula": GOTO 1410
1280 IF T$="1" THEN TY$="Single star": GOTO 1410
1290 IF T$="2" THEN TY$="Double star": GOTO 1410
1300 IF T$="A" THEN TY$="Asterism": GOTO 1410
1310 IF T$="P" THEN TY$="Planetary nebula": GOTO 1410
1320 IF T$="?" THEN TY$="Unknown or nonexistent": GOTO 1410
1330 IF T$="G" THEN TY$="Globular cluster": GOTO 1410
1340 IF T$="O" THEN TY$="Open cluster": GOTO 1410
1350 IF T$="5" THEN TY$="Double star?": GOTO 1410
1360 IF T$="C" THEN TY$="Cluster with nebulosity": GOTO 1410
1370 IF T$="3" THEN TY$="Triple star": GOTO 1410
1380 IF T$="K" THEN TY$="Knot in galaxy": GOTO 1410
1390 IF T$="D" THEN TY$="Plate defect": GOTO 1410
1400 IF T$="4" THEN TY$="Single star?"
1410 PRINT N$;"  ";RA$;"  ";DC$;"  ";S$;"  ";B$;"  ";TY$
1420 RETURN
1430 REM
1440 REM  EXPRESS R.A. AS A STRING
1450 REM   -- ENTER WITH H,M; EXIT WITH P0$
1460 REM
1470 P=100000!+H*1000+M*10: P=INT(P)
1480 P0$=STR$(P): P0$=RIGHT$(P0$,5)
1490 IF LEFT$(P0$,1)="0" THEN P0$=" "+RIGHT$(P0$,4)
1500 RETURN
1510 REM
1520 REM  SEEK SUBROUTINE -- ENTER WITH P0$; EXIT WITH I SET TO
1530 REM       THE RECORD NUMBER JUST BEFORE THE DESIRED R.A.
1540 REM
1550 I0=1: I9=13226
1560 I=INT(I9/2): REM  START IN MIDDLE OF FILE
1570 D=I
1580 D=INT(D/2)
1590 IF D=0 THEN 1640
1600 GET#1,I
1610 S=1: IF P$>P0$ THEN S=-1
1620 I=I+S*D
1630 GOTO 1580
1640 REM
1650 IF I=I9 THEN 1680
1660 IF P$<P0$ THEN I=I+1: GET#1,I: GOTO 1650
1670 I=I-1: IF I>0 THEN GET#1,I
1680 IF P$>=P0$ AND I>1 THEN 1670
1690 RETURN
1700 REM
1710 REM   COMMON EXIT
1720 CLOSE
1730 END
